apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

group = 'com.projectkaiser.scm'
version = '1.0-SNAPSHOT'


repositories {
	mavenCentral()
}

sourceSets {
	abstractTest 
	api {
		java {
			srcDir 'src/main/java/com/projectkaiser/scm/vcs/api'
		}
	}
}

sourceSets.all { set ->
    def jarTask = task("${set.name}Jar", type: Jar) {
        baseName = "$set.name"
        from set.output
    }

    def sourcesJarTask = task("${set.name}SourcesJar", type: Jar) {
        baseName = "$set.name"
		classifier = 'sources'
        from set.allSource
    }
    
    artifacts {
        archives jarTask
	    archives sourcesJarTask
    }
}

task apiJavadocTask(type: Javadoc) {
	source = sourceSets.api.allJava
	destinationDir = file("${buildDir}/docs/api")
	classpath = sourceSets.api.compileClasspath
}

task abstractTestJavadocTask(type: Javadoc) {
    source = sourceSets.abstractTest.allJava
    classpath = sourceSets.abstractTest.compileClasspath
    destinationDir = file("${buildDir}/docs/abstractTest")
}

task abstractTestJavadocJar(type: Jar, dependsOn: abstractTestJavadocTask) {
	baseName = sourceSets.abstractTest.name
	classifier = 'javadoc'
    from abstractTestJavadocTask.destinationDir
}

task apiJavadocJar(type: Jar, dependsOn: apiJavadocTask) {
	baseName = sourceSets.api.name
	classifier = 'javadoc'
    from apiJavadocTask.destinationDir
}

artifacts {
	archives apiJavadocJar
	archives abstractTestJavadocJar
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.62-beta'
    
    compile 'commons-logging:commons-logging:1.2'
    compile 'commons-io:commons-io:2.4'
    
    apiCompile 'commons-logging:commons-logging:1.2'
    apiCompile 'commons-io:commons-io:2.4'
    
    abstractTestCompile 'junit:junit:4.12'
    abstractTestCompile 'org.mockito:mockito-core:2.0.62-beta'
    abstractTestCompile sourceSets.api.output
    abstractTestCompile 'commons-io:commons-io:2.4'
    
    runtime configurations.apiRuntime
    runtime configurations.abstractTestRuntime
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}
	
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

install {
    repositories.mavenInstaller {
		addFilter('abstractTest') { artifact, file -> artifact.name.endsWith('abstractTest') }
		addFilter('api') { artifact, file -> artifact.name.endsWith('main') }
    }
}